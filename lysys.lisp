;;;; lysys.lisp

(in-package #:lysys)

(defun deg-angle (deg) (* (/ pi 180.0) deg))

;; Database of L-systems using symbol plists.

(defmacro define-l-system (sym &key axiom rules angle)
  `(setf (symbol-plist ',sym)
	 (list :axiom ',axiom
	  :rules ',rules
	  :angle (deg-angle ,angle))))

(define-l-system rings
  :axiom (F + F + F + F)
  :rules ((F -> F F + F + F + F + F + F - F))
  :angle 90.0)

(define-l-system quadratic-koch-1
  :axiom (F + F + F + F)
  :rules ((F -> F + F - F - F F + F + F - F))
  :angle 90.0)

(define-l-system tatami
  :axiom (F + F + F + F)
  :rules ((F -> F F + F - F + F + F F))
  :angle 90.0)

(define-l-system square-sierpinski
  :axiom (F + X F + F + X F)
  :rules ((X -> X F - F + F - X F + F + X F - F + F - X))
  :angle 90.0)

(define-l-system hilbert
  :axiom (X)
  :rules ((X -> - Y F + X F X + F Y -)
	  (Y -> + X F - Y F Y - F X +))
  :angle 90.0)

(define-l-system sierpinski-triangle
  :axiom (Y F)
  :rules ((X -> Y F + X F + Y)
	  (Y -> X F - Y F - X))
  :angle 60.0)

(define-l-system hex-gosper
  :axiom (X F)
  :rules ((X -> X + Y F + + Y F - F X - - F X F X - Y F +)
	  (Y -> - F X + Y F Y F + + Y F + F X - - F X - Y))
  :angle 60.0)

(define-l-system levy-curve
  :axiom (F)
  :rules ((F -> - F + + F -))
  :angle 45.0)

(define-l-system anklet
  :axiom (- X - - X)
  :rules ((X -> X F X - - X F X))
  :angle 45.0)

(define-l-system fern
  :axiom (X)
  :rules ((F -> F F)
	  (X -> F - pu pu X po + X po  + F pu + F X po - X))
  :angle 22.5)

(define-l-system fern2
  :axiom (F)
  :rules ((F -> F F + pu + F - F - F po - pu - F + F + F po))
  :angle 22.5)



(defun apply-substitution (sentence rules)
  (alexandria:flatten 
   (loop for u in sentence
	 collecting (if (assoc u rules)
			(cddr (assoc u rules)) 
			u))))

(defun compute-sentence-level (axiom rules n)
  (let ((sentence axiom))
    (loop for k from 1 to n
	  do (setf sentence
		   (apply-substitution sentence rules)))
    sentence))

(defun sentence-to-string (sentence)
  (format nil "窿箦铘孱沐┅ㄤ彐躅戾鲥飙瞽篝蜷铉飙簌篝屙瞟箦铘孱沐麸篝蜷铉ㄣ镯瘐翦箦铘孱沐戾鲥ㄧ弭飙簌篝屙横轱愆ㄧ弭飙簌篝屙候蹯弩瞟┅换物铄邃麸脲屦翳疳翳鲥螋殂弩轭礤盹蝙换术篝黩轸翳屙秕镱怡镱瀹ㄤ彐躅黩轸瀛秕秕篝狒濠ㄦ矧磲秕狺アㄦ轵篝篝狒濠箦泔钿篝狒濠┅ㄤ彐躅疳蝮瀛箦铘孱沐犷洵黩轸瀛疳翳箦铘孱沐犷珈骈戾钺礤戾è沲蝌孱舡篝狒扉篝爱爱爱癌篝徙铋飑鏖翳镳孱骈戾秕骈戾钺礤轰轵邈糸镱猴豸瘐洪姝屮轶趔后躔弪箦溴黩轸瀛秕秕沲蝌孱舡篝狒濠祜镳骘轭箦铘孱沐滹痱镧ㄣ狍ㄆ痱镧ㄩ钽ㄦ轵篝沲蝌孱舡篝狒濠ㄣ矬翳轵沲蝌孱舡篝狒濠┅ㄩ钽箦泔钿沲蝌孱舡篝狒濠箝翳轵沲蝌孱舡篝狒濠┅黩轸瀛秕秕沲蝌孱舡篝狒濠┅ǐㄩ钽翳轵沲蝌孱舡篝狒濠犷珈濠ōㄤ邈翳轵沲蝌孱舡篝狒濠犷珈濠瘐瘐箬ㄣ镳箦沲蝌孱舡篝狒濠篝徙氅痫箦翩沲蝌孱舡篝狒痫篝徙氅┅┅┅┅ㄤ彐躅蝓瞽飙簌篝屙飙簌篝屙镳糸镱犰ㄦ殪孱犴疳翳鲥螋殂弩溽簪┅ㄤ弩趄蹉趱蜷铉忾钿é脲狲轱蝓戾犷珈濠簌礅镬痨轶飙簌篝屙戾è箦铘孱沐ㄣ镯瘐翦箦铘孱沐戾鲥狲轱蝓戾瞟┅疳蝮瀛箦铘孱沐犷洵黩轸瀛疳翳箦铘孱沐犷珈骈戾钺礤┅┅